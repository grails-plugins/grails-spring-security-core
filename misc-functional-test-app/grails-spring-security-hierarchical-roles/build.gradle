buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.14.2"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
    }
}

version "0.1"
group "grails.spring.security.hierarchical.roles"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.grails-gsp"
apply plugin:"asset-pipeline"
apply plugin:"org.grails.grails-gsp"


repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:events"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.5.Final"
    compile "org.grails.plugins:gsp"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "com.h2database:h2"
    runtime "org.apache.tomcat:tomcat-jdbc"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.14.2"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
}

apply from: '../../gradle/ssc.gradle'

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
}

assets {
    minifyJs = true
    minifyCss = true
}


integrationTest {
    systemProperties System.properties
}

task s2Quickstart {
    ext.commands = ['s2-quickstart demo User Role', 's2-create-role-hierarchy-entry demo.RoleHierarchyEntry']
    doLast {
        for ( String cmd : commands ) {
            def sout = new StringBuilder(), serr = new StringBuilder()
            def proc = "grails $cmd".execute()
            proc.consumeProcessOutput(sout, serr)
            proc.waitForOrKill(20000)
            println "$sout $serr"
        }
    }
}
task resetResourcesGroovy {
    doLast {
        new File("${project.projectDir}/grails-app/conf/spring/resources.groovy").text = 'beans = {\n}'
    }
}
task deleteS2QuickstartGeneratedFiles {
    dependsOn 'resetResourcesGroovy'
    doLast {
	delete("${project.projectDir}/grails-app/conf/application.groovy")
	delete("${project.projectDir}/grails-app/domain/demo")
	delete("${project.projectDir}/src/main")
    }
}
s2Quickstart.mustRunAfter 'deleteS2QuickstartGeneratedFiles'
