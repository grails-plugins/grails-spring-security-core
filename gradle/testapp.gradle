File pluginDir = file('.').parentFile
File versionTxt
while (true) {
	versionTxt = new File(pluginDir, 'version.txt')
	if (versionTxt.exists()) {
		break
	}
	pluginDir = pluginDir.parentFile
}

apply plugin: 'org.grails.grails-web'

apply from: new File(pluginDir, 'gradle/common.gradle').path

project.ext.pluginVersion = versionTxt.text.trim()
project.ext.pluginName = pluginDir.name - 'grails-'
project.ext.seleniumVersion = '2.53.1'

dependencies {
	compile "org.grails.plugins:$pluginName:$pluginVersion"
	compile 'com.h2database:h2:1.4.190'
	compile 'org.grails:grails-dependencies'
	compile 'org.springframework.boot:spring-boot-autoconfigure'
	compile 'org.springframework.boot:spring-boot-starter-logging'
	compile 'org.springframework.boot:spring-boot-starter-tomcat'
	console 'org.grails:grails-console'
	runtime 'org.grails.plugins:asset-pipeline'
	testCompile 'org.gebish:geb-core:1.1.1'
	testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
	testRuntime "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
	testCompile "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
	testCompile "com.codeborne:phantomjsdriver:1.3.0"
	['chrome', 'firefox'].each { String name ->
		testCompile "org.seleniumhq.selenium:selenium-${name}-driver:$seleniumVersion"
	}
	testCompile 'org.grails.plugins:geb'
}
